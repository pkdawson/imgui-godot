#!/usr/bin/env python

IMGUI_PATH = "../../../gdext/imgui"
IMGUI_GODOT_INCLUDE = "../../../addons/imgui-godot/include"

env = SConscript("../../../gdext/godot-cpp/SConstruct")
env = env.Clone()

env.Append(CPPPATH=["src/"])
sources = Glob("src/*.cpp")

# Dear ImGui
sources += Glob(f"{IMGUI_PATH}/*.cpp")
env.Append(CPPDEFINES=['IMGUI_USER_CONFIG="\\"imconfig-godot.h\\""'])
env.Append(CPPPATH=[IMGUI_PATH, IMGUI_GODOT_INCLUDE])

if env["platform"] == "macos":
    library = env.SharedLibrary(
        "project/bin/libgdexample.{}.{}.framework/libgdexample.{}.{}".format(
            env["platform"], env["target"], env["platform"], env["target"]
        ),
        source=sources,
    )
else:
    library = env.SharedLibrary(
        "project/bin/libgdexample{}{}".format(env["suffix"], env["SHLIBSUFFIX"]),
        source=sources,
    )

Default(library)
