#!/usr/bin/env python
from glob import glob
from pathlib import Path

env = SConscript("godot-cpp/SConstruct")
env.Append(CPPDEFINES=['IMGUI_USER_CONFIG="\\"imconfig-godot.h\\""', "IGN_EXPORT"])
env.Append(CPPPATH=["src/", "imgui/", "include/", "gen/"])
env.Replace(CXXFLAGS=str(env["CXXFLAGS"]).replace("c++17", "c++20"))

sources = Glob("src/*.cpp") + Glob("imgui/*.cpp") + Glob("gen/*.cpp")

(extension_path,) = glob("proj/addons/*/*.gdextension")
addon_path = Path(extension_path).parent
project_name = Path(extension_path).stem

config = "release" if env["target"] == "template_release" else "debug"

libpath = env["LIBPATH"]
libs = [env["LIBS"]]

windows = env["platform"] == "windows"
linux = env["platform"] == "linux"

if config == "release":
    if windows:
        env.Append(CPPDEFINES=["NDEBUG"])

if windows:
    triplet = "x64-windows-static-md"
elif linux:
    triplet = "x64-linux"
else:
    triplet = "arm64-osx"

env.Append(CPPPATH=["imgui/misc/freetype/"])
env.Append(CPPPATH=[f"vcpkg_installed/{triplet}/include"])
env.Append(CPPDEFINES=["IMGUI_ENABLE_FREETYPE"])
sources += Glob("imgui/misc/freetype/*.cpp")
libpath += [f"vcpkg_installed/{triplet}/lib"]
libs += [
    "freetype",
    "bz2",
    "libpng16" if windows else "png16",
    "zlib" if windows else "z",
    "brotlidec-static",
    "brotlicommon-static",
]

if env["platform"] == "macos":
    library = env.SharedLibrary(
        "{0}/bin/lib{1}.{2}.{3}.framework/{1}.{2}.{3}".format(
            addon_path,
            project_name,
            env["platform"],
            config,
        ),
        source=sources,
        LIBPATH=libpath,
        LIBS=libs,
    )
else:
    library = env.SharedLibrary(
        "{}/bin/lib{}.{}.{}.{}{}".format(
            addon_path,
            project_name,
            env["platform"],
            config,
            env["arch"],
            env["SHLIBSUFFIX"],
        ),
        source=sources,
        LIBPATH=libpath,
        LIBS=libs,
    )

Default(library)
